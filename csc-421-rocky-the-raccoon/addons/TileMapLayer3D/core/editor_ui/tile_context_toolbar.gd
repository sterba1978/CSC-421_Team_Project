# =============================================================================
# PURPOSE: Context UI component for TileMapLayer3D editor plugin
# =============================================================================
# This class manages the side toolbar with tile operation buttons:
#   - Rotation buttons (Q/E)
#   - Tilt button (R)
#   - Reset button (T)
#   - Flip button (F)
#   - Status display (current rotation/tilt/flip state)
@tool
class_name TileContextToolbar
extends HBoxContainer

# =============================================================================
# SECTION: SIGNALS
# =============================================================================

## Emitted when rotation is requested (direction: +1 CW, -1 CCW)
signal rotate_btn_pressed(direction: int)

## Emitted when tilt cycling is requested (shift: bool for reverse)
signal tilt_btn_pressed(reverse: bool)

## Emitted when reset to flat is requested
signal reset_btn_pressed()

## Emitted when face flip is requested
signal flip_btn_pressed()

##Emmited when SmartSelect Mode is changed
signal smart_select_mode_changed(smart_mode: GlobalConstants.SmartSelectionMode)

## Emitted when SmartSelect operations REPLACE/DELETE buttons are pressed -# FUTURE FEATURE #TODO # DEBUG
signal smart_select_operation_btn_pressed(smart_mode_operation: GlobalConstants.SmartSelectionOperation)
##	Emitted when mesh mode is selected from dropdown
signal mesh_mode_selection_changed(mesh_mode: GlobalConstants.MeshMode)
## Emitted when mesh mode depth spinbox value changes (for BOX/PRISM depth scaling)
signal mesh_mode_depth_changed(depth: float)

# Emitted when autotile mesh mode changes (FLAT_SQUARE or BOX_MESH only)
signal autotile_mesh_mode_changed(mesh_mode: int)
# Emitted when autotile depth scale changes (for BOX/PRISM mesh modes)
signal autotile_depth_changed(depth: float)

# =============================================================================
# SECTION: MEMBER VARIABLES
# =============================================================================

## Main UI Node Groups to show/hide based on mode
@onready var manual_mode_group: FlowContainer = %ManualModeGroup
@onready var smart_select_group: HBoxContainer = %SmartSelectGroup
@onready var auto_tile_mode_group: FlowContainer = %AutoTileModeGroup


## Rotate Right button (Q)
@onready var _rotate_right_btn: Button = %RotateRightBtn
## Rotate Left button (E)
@onready var _rotate_left_btn: Button = %RotateLeftBtn
## Tilt button (R)
@onready var _cycle_tilt_btn: Button = %CycleTiltBtn
## Reset button (T)
@onready var _reset_orientation_btn: Button = %ResetOrientationBtn
## Flip button (F)
@onready var _flip_face_btn: Button = %FlipFaceBtn
## Status label
@onready var _status_label: Label = %StatusLabel
# ## SmartSelect button (G) - FUTURE FEATURE #TODO # DEBUG
# @onready var smart_select_btn: Button = $SmartSelectBtn

## Smart selection mode - determines how the smart selection algorithm behaves
## SINGLE_PICK = 0, # Pick tiles individually - Additive selection
## CONNECTED_UV = 1, # Smart Selection of all neighbours that share the same UV - Tile Texture
## CONNECTED_NEIGHBOR = 2, # Smart Selection of all neighbours on the same plane and rotation
@onready var smart_mode_option_btn: OptionButton = %SmartSelectionModeOptBtn
@onready var smart_select_replace_btn: Button = %SmartSelectReplaceBtn
@onready var smart_select_delete_btn: Button = %SmartSelectDeleteBtn


# @onready var tile_size_x: SpinBox = %TileSizeX
# @onready var tile_size_y: SpinBox = %TileSizeY

@onready var mesh_mode_dropdown: OptionButton = %MeshModeDropdown
@onready var mesh_mode_depth_spin_box: SpinBox = %MeshModeDepthSpinBox
@onready var auto_tile_mode_dropdown: OptionButton = %AutoTileModeDropdown
@onready var auto_tile_detph_spin_box: SpinBox = %AutoTileDetphSpinBox


@onready var mesh_mode_label: Label = %MeshModeLabel
@onready var mesh_mode_depth_lbl: Label = %MeshModeDepthLbl
# @onready var tile_size_label: Label = $ManualModeGroup/TileSizeControls/TileSizeLabel
@onready var tile_world_pos_label: Label = %TileWorldPosLabel
@onready var tile_grid_pos_label: Label = %TileGridPosLabel

## UI Variables
var _updating_ui: bool = false

# =============================================================================
# SECTION: INITIALIZATION
# =============================================================================

func _init() -> void:
	name = "TileContextToolbar"


func _ready() -> void:
	prepare_ui_components()
	

func prepare_ui_components() -> void:
	#Rotate Right (Q)
	_rotate_right_btn.pressed.connect(_on_rotate_right_pressed)
	GlobalUtil.apply_button_theme(_rotate_right_btn, "RotateRight", GlobalConstants.BUTTOM_CONTEXT_UI_SIZE)

	#Rotate Left (E)
	_rotate_left_btn.pressed.connect(_on_rotate_left_pressed)
	GlobalUtil.apply_button_theme(_rotate_left_btn, "RotateLeft", GlobalConstants.BUTTOM_CONTEXT_UI_SIZE)

	# Tilt (R)
	_cycle_tilt_btn.pressed.connect(_on_tilt_pressed)
	GlobalUtil.apply_button_theme(_cycle_tilt_btn, "FadeCross", GlobalConstants.BUTTOM_CONTEXT_UI_SIZE)

	# Reset (T)
	_reset_orientation_btn.pressed.connect(_on_reset_pressed)
	GlobalUtil.apply_button_theme(_reset_orientation_btn, "EditorPositionUnselected", GlobalConstants.BUTTOM_CONTEXT_UI_SIZE)

	# Flip (F)
	_flip_face_btn.toggled.connect(_on_flip_toggled)
	GlobalUtil.apply_button_theme(_flip_face_btn, "ExpandTree", GlobalConstants.BUTTOM_CONTEXT_UI_SIZE)

	# #SmartSelect button (G) - FUTURE FEATURE #TODO # DEBUG
	# smart_select_btn.pressed.connect(_on_smart_select_pressed)
	# apply_button_theme(smart_select_btn, "EditPivot")

	#SmartSelect Replace button - FUTURE FEATURE #TODO # DEBUG
	smart_select_replace_btn.pressed.connect(_on_smart_select_replace_pressed)
	GlobalUtil.apply_button_theme(smart_select_replace_btn, "Loop", GlobalConstants.BUTTOM_CONTEXT_UI_SIZE) #Loop

	#SmartSelect Delete button - FUTURE FEATURE #TODO # DEBUG
	smart_select_delete_btn.pressed.connect(_on_smart_select_delete_pressed)
	GlobalUtil.apply_button_theme(smart_select_delete_btn, "Remove", GlobalConstants.BUTTOM_CONTEXT_UI_SIZE) # Remove

	var ui_scale: float = GlobalUtil.get_editor_ui_scale()

	#SmartSelect Mode - FUTURE FEATURE #TODO # DEBUG
	smart_mode_option_btn.item_selected.connect(_on_smart_select_mode_changed)
	smart_mode_option_btn.add_theme_font_size_override("font_size", int(10 * ui_scale))
	smart_mode_option_btn.custom_minimum_size.x = 115 * ui_scale

	# --- Status Label ---
	_status_label.text = "0°"
	_status_label.horizontal_alignment = HORIZONTAL_ALIGNMENT_LEFT
	_status_label.label_settings.font_size = int(10 * ui_scale)

	# --- All other Labels ---
	# tile_size_label.label_settings.font_size = int(8 * ui_scale)
	tile_world_pos_label.label_settings.font_size = int(8 * ui_scale)
	tile_grid_pos_label.label_settings.font_size = int(8  * ui_scale)
	mesh_mode_label.label_settings.font_size = int(10 * ui_scale)
	mesh_mode_depth_lbl.label_settings.font_size = int(10 * ui_scale)

	# --- Spinbox controls  ---
	# tile_size_x.get_line_edit().add_theme_font_size_override("font_size", int(10 * ui_scale))
	# tile_size_y.get_line_edit().add_theme_font_size_override("font_size", int(10 * ui_scale))
	mesh_mode_depth_spin_box.get_line_edit().add_theme_font_size_override("font_size", int(10 * ui_scale))

	mesh_mode_dropdown.item_selected.connect(_on_mesh_mode_selected)
	mesh_mode_depth_spin_box.value_changed.connect(_on_mesh_mode_depth_changed)

	auto_tile_mode_dropdown.item_selected.connect(_on_auto_tile_mode_selected)
	auto_tile_detph_spin_box.value_changed.connect(_on_auto_tile_depth_changed)


## Set flip button state
func set_flipped(flipped: bool) -> void:
	_updating_ui = true
	_flip_face_btn.button_pressed = flipped
	_updating_ui = false


## Get flip state
func is_flipped() -> bool:
	return _flip_face_btn.button_pressed if _flip_face_btn else false


## Update the status display
## @param rotation_steps: Current rotation (0-3 = 0°, 90°, 180°, 270°)
## @param tilt_index: Current tilt index (0 = flat)
## @param is_flipped: Whether face is flipped
func update_status(rotation_steps: int, tilt_index: int, is_flipped: bool) -> void:
	if not _status_label:
		return

	var rotation_deg: int = rotation_steps * 90
	var parts: PackedStringArray = []

	# Rotation
	parts.append(str(rotation_deg) + "°")

	# Tilt indicator
	if tilt_index > 0:
		parts.append("T" + str(tilt_index))

	# Flip indicator
	if is_flipped:
		parts.append("F")

	_status_label.text = " ".join(parts)

	# Update flip button state
	_updating_ui = true
	_flip_face_btn.button_pressed = is_flipped
	_updating_ui = false



func sync_from_settings(tilemap_settings: TileMapLayerSettings) -> void:
	if not tilemap_settings:
		return
	_updating_ui = true

	# UI Items to sync:
	smart_mode_option_btn.select(tilemap_settings.smart_select_mode)

	print("CONTEXT_TOOLBAR: Syncing mesh mode from settings: ", tilemap_settings.mesh_mode)
	mesh_mode_dropdown.selected = tilemap_settings.mesh_mode
	mesh_mode_depth_spin_box.value = tilemap_settings.current_depth_scale
	auto_tile_mode_dropdown.selected = tilemap_settings.autotile_mesh_mode
	auto_tile_detph_spin_box.value = tilemap_settings.autotile_depth_scale


	# Sync visibility from mode + smart select state
	match tilemap_settings.main_app_mode:
		GlobalConstants.MainAppMode.MANUAL:
			manual_mode_group.visible = true
			smart_select_group.visible = false
			auto_tile_mode_group.visible = false
			self.visible = true
		GlobalConstants.MainAppMode.AUTOTILE:
			manual_mode_group.visible = false
			smart_select_group.visible = false
			auto_tile_mode_group.visible = true
			self.visible = true
		GlobalConstants.MainAppMode.MANUAL_SMART_SELECT:
			manual_mode_group.visible = false
			smart_select_group.visible = true
			auto_tile_mode_group.visible = false
			self.visible = true
		GlobalConstants.MainAppMode.SETTINGS:
			self.visible = false
		_:
			manual_mode_group.visible = true
			smart_select_group.visible = true
			auto_tile_mode_group.visible = true
			self.visible = true

	_updating_ui = false


## Updates tile position display with both world and grid coordinates
## @param world_pos: Absolute world-space position
## @param grid_pos: Grid coordinates within the TileMapLayer3D node
func update_tile_position(world_pos: Vector3, grid_pos: Vector3, current_plane:int) -> void:

	match current_plane:
		0, 1: 
			grid_pos.y += GlobalConstants.GRID_ALIGNMENT_OFFSET.y # Y plane
		2, 3: 
			grid_pos.z += GlobalConstants.GRID_ALIGNMENT_OFFSET.z # Z plane
		4, 5: 
			grid_pos.x += GlobalConstants.GRID_ALIGNMENT_OFFSET.x # X plane
		_: 
			pass

	# print("plane is:" , current_plane)
	if tile_world_pos_label:
		tile_world_pos_label.text = "World: (%.1f, %.1f, %.1f)" % [world_pos.x, world_pos.y, world_pos.z]
	if tile_grid_pos_label:
		tile_grid_pos_label.text = "Grid: (%.1f, %.1f, %.1f)" % [grid_pos.x, grid_pos.y, grid_pos.z]
# =============================================================================
# SECTION: SIGNAL HANDLERS
# =============================================================================

func _on_rotate_right_pressed() -> void:
	rotate_btn_pressed.emit(-1)


func _on_rotate_left_pressed() -> void:
	rotate_btn_pressed.emit(+1)


func _on_tilt_pressed() -> void:
	# Check if shift is held for reverse tilt
	var reverse: bool = Input.is_key_pressed(KEY_SHIFT)
	tilt_btn_pressed.emit(reverse)


func _on_reset_pressed() -> void:
	reset_btn_pressed.emit()


func _on_flip_toggled(pressed: bool) -> void:
	if _updating_ui:
		return
	flip_btn_pressed.emit()


func _on_mesh_mode_selected(index: int) -> void:
	if _updating_ui:
		return
	print("CONTEXT_PANEL: Mesh mode selected index: ", index)
	mesh_mode_selection_changed.emit(mesh_mode_dropdown.get_selected_id())

func _on_mesh_mode_depth_changed(value: float) -> void:
	if _updating_ui:
		return
	mesh_mode_depth_changed.emit(value)

func _on_auto_tile_mode_selected(index: int) -> void:
	if _updating_ui:
		return
	print("CONTEXT_PANEL: AutoTile mode selected index: ", index)
	# FUTURE FEATURE - TODO - DEBUG
	autotile_mesh_mode_changed.emit(auto_tile_mode_dropdown.get_selected_id())

func _on_auto_tile_depth_changed(value: float) -> void:
	if _updating_ui:
		return
	# FUTURE FEATURE - TODO - DEBUG
	print("CONTEXT_PANEL: AutoTile depth changed to: ", value)
	autotile_depth_changed.emit(value)

func _on_smart_select_mode_changed(mode: GlobalConstants.SmartSelectionMode) -> void:
	# FUTURE FEATURE - TODO - DEBUG
	if _updating_ui:
		return
	
	smart_select_mode_changed.emit(smart_mode_option_btn.get_selected_id())
	# print("Smart Select mode changed - Mode is: ", mode)

func _on_smart_select_replace_pressed() -> void:
	# FUTURE FEATURE - TODO - DEBUG
	print("Smart Select Replace button pressed")
	smart_select_operation_btn_pressed.emit(GlobalConstants.SmartSelectionOperation.REPLACE)

	pass


func _on_smart_select_delete_pressed() -> void:
	# FUTURE FEATURE - TODO - DEBUG
	print("Smart Select Delete button pressed")
	smart_select_operation_btn_pressed.emit(GlobalConstants.SmartSelectionOperation.DELETE)

	pass
